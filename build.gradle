import org.codehaus.groovy.runtime.GStringImpl

buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

apply plugin: 'idea'
idea {
    module {
        inheritOutputDirs = true
    }
    project {
        languageLevel = '1.8' //due to a bug in gradle or IDEA, this doesn't work :(
    }
}

task dlImmersiveEngineering(type: LibDownloader) {
    sourceUrl = 'https://minecraft.curseforge.com/projects/immersive-engineering/files/2501821/download'
    target = file('libs/ImmEngineering.jar')
}

task dlAppliedEnergistics(type: LibDownloader) {
    sourceUrl = 'https://minecraft.curseforge.com/projects/applied-energistics-2/files/2503759/download'
    target = file('libs/AppEnergistics.jar')
}

task dlRefinedStorage(type: LibDownloader) {
    sourceUrl = 'https://minecraft.curseforge.com/projects/refined-storage/files/2509264/download'
    target = file('libs/RefStorage.jar')
}

setupDecompWorkspace.dependsOn dlImmersiveEngineering, dlAppliedEnergistics, dlRefinedStorage
setupDevWorkspace.dependsOn dlImmersiveEngineering, dlAppliedEnergistics, dlRefinedStorage
setupCiWorkspace.dependsOn dlImmersiveEngineering, dlAppliedEnergistics, dlRefinedStorage

version = "1.12.2-1.2.0"
group = "de.sanandrew.mods"
archivesBaseName = "ImmersiveCables"

sourceCompatibility = targetCompatibility = "1.8" // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.1.2555"
    runDir = "run"
    mappings = "snapshot_20170716"
}

repositories {
  maven {
    url = "http://dvs1.progwml6.com/files/maven"
  }
}

dependencies {
  deobfCompile "mezz.jei:jei_1.12:4.7.1.69:api"
  runtime "mezz.jei:jei_1.12:4.7.1.69"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

sourceJar {
    from sourceSets.main.java
    appendix = 'src'
    archiveName = "${baseName}-${version}-${appendix}.jar"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
    archiveName = "${baseName}-${version}-${appendix}.jar"
}

configurations {
//    deployDev
}

artifacts {
    archives deobfJar
    archives sourceJar
    archives jar
//    deployDev deobfJar
}

project.ext {
    releaseRepoUrl = "file://${project(':').projectDir}/maven-repo/" as GStringImpl
}

class LibDownloader extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
       ant.get(src: sourceUrl, dest: target)
    }
}